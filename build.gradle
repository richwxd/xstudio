group 'com.xstudio'
version '1.0.0-SNAPSHOT'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: "maven-publish"

    sourceCompatibility = 1.8

    repositories {
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        maven {
            url 'http://maven.aliyun.com/nexus/content/repositories/jcenter'
        }
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compile "org.projectlombok:lombok:1.18.10"
        annotationProcessor "org.projectlombok:lombok:1.18.10"

        // https://mvnrepository.com/artifact/com.alibaba/fastjson
        compile group: 'com.alibaba', name: 'fastjson', version: '1.2.62'

        testCompile group: 'junit', name: 'junit', version: '4.12'

        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    }

    task allTask {
        println project.name
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    // 打包源码
    task sourceJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourceJar
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/xbeeant/xstudio")
                credentials {
                    username = "huangxb0512@gmail.com"
                    password = ""
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar
                artifact javadocJar
                pom {
                    description = "xstudio spring & java tool"
                    url = "https://maven.pkg.github.com/xbeeant/xstudio"
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://mit-license.org/"
                        }
                    }
                    developers {
                        developer {
                            id = "beeant0512"
                            name = "Xiaobiao Huang"
                            email = "huangxb0512@gmail.com"
                        }
                    }
                    scm {
                        connection = "https://maven.pkg.github.com/xbeeant/xstudio"
                        developerConnection = "https://maven.pkg.github.com/xbeeant/xstudio"
                        url = "https://maven.pkg.github.com/xbeeant/xstudio"
                    }
                }
            }
        }
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
        options {
            encoding "UTF-8"
            charSet 'UTF-8'
            author true
            version true
        }
    }
}